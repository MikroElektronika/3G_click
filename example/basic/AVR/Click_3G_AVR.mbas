'
'Example for 3G Click
'
'    Date          : Nov 2017.
'    Author        : MikroE Team
'
'Test configuration AVR :
'    
'    MCU              : ATMEGA32
'    Dev. Board       : EasyAVR v7 
'    AVR Compiler ver : v7.0.0.0
'
'---
'
'Description :
'
'The application is composed of three sections :
'
'- System Initialization - Initializes all necessary GPIO pins, UART used for
'the communcation with GSM module and UART used for infromation logging
'- Application Initialization - Initializes driver, power on module and sends few
'command for the default module configuration
'- Application Task - running in parallel core state machine and and checks for call flag.
'If call detected parser will hang up call.
'
'Additional Functions :
'
'All additional functions such as timer initialization and default handler. 
''
'Notes :
'
'- Echo must be turned off for succefull parsing. ( ATE0 )
'- UART polling works much better with HFC enabled.
'- In case of usage of other MCUs Timer initialization must be adjusted according to your MCU.
'
'
program Click_3G_AVR

include Click_3G_types
include Click_3G_config
include Click_3G_timer

dim
    ' COMMANDS
    C3G_AT as string[3]
    C3G_HANGUP as string[4]
    C3G_ECHO_OFF as string[5]
    C3G_FORMAT_DEFAULT as string[10]
    C3G_HFC_ENABLE as string[11]

    ' CALLER FLAG
    callFlag as uint8_t

sub procedure c3g_default_handler(dim rsp as ^uint8_t, dim evArgs as ^uint8_t) 
dim
    ptr_rsp as ^char
    
    ptr_rsp = rsp + 2
    
    if ( ptr_rsp^ = 82 ) then
        callFlag = 1
    end if

    mikrobus_logWrite( ptr_rsp - 2, _LOG_TEXT )
    
end sub

sub procedure systemInit() 

    C3G_AT = "AT"
    C3G_HANGUP = "ATH"
    C3G_ECHO_OFF = "ATE0"
    C3G_FORMAT_DEFAULT = "AT+CMGF=1"
    C3G_HFC_ENABLE = "AT+IFC=2,2"

    callFlag = 0

    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_AN_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_PWM_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_INT_PIN, _GPIO_INPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_RST_PIN, _GPIO_OUTPUT )
    mikrobus_gpioInit( _MIKROBUS1, _MIKROBUS_CS_PIN, _GPIO_OUTPUT )    

    mikrobus_uartInit(_MIKROBUS1, @_C3G_UART_CFG[0]) 
'    mikrobus_logInit(_MIKROBUS2, 9600) 

end sub

sub procedure applicationInit() 

' TIMER INIT
    c3g_configTimer()

' DRIVER INIT
    c3g_uartDriverInit(T_C3G_P(@_MIKROBUS1_GPIO), T_C3G_P(@_MIKROBUS1_UART)) 
    c3g_coreInit(@c3g_default_handler, 1500)

' MODULE POWER ON
    c3g_hfcEnable(true) 
    c3g_modulePower(true) 

' MODULE INIT
    c3g_cmdSingle(@C3G_AT[0]) 
    c3g_cmdSingle(@C3G_AT[0]) 
    c3g_cmdSingle(@C3G_AT[0]) 
    c3g_cmdSingle(@C3G_ECHO_OFF[0]) 
    c3g_cmdSingle(@C3G_HFC_ENABLE[0]) 
    c3g_cmdSingle(@C3G_FORMAT_DEFAULT[0]) 

end sub

sub procedure applicationTask() 

    c3g_process() 

    if ((0 <> callFlag)) then 
    
        c3g_cmdSingle(@C3G_HANGUP[0]) 
        Delay_ms(3000) 

        callFlag = 0 
    end if

end sub

main :

    systemInit() 
    applicationInit() 
    while (1) 
        applicationTask() 
    wend

end.